import java.nio.file.Files

plugins {
	id "fabric-loom" version "1.0.+"
	id "io.github.juuxel.loom-quiltflower" version "1.+" // Quiltflower, a better decompiler
	id "io.github.p03w.machete" version "1.+" // automatic jar compressing on build
	id "com.modrinth.minotaur" version "2.+"
	id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
group = project.maven_group

String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
version = project.mod_version + (buildNumber != null ? "-build.$buildNumber" : "") + "+mc$minecraft_version"

repositories {
	maven { url = "https://maven.quiltmc.org/repository/release" }
	maven { url = "https://api.modrinth.com/maven" }
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url = "https://aperlambda.github.io/maven" }
	maven { url = "https://maven.kotlindiscord.com/repository/terraformers/" }
	maven { url = "https://cursemaven.com" }
	maven { url =  "https://jitpack.io" }
	maven { url = "https://maven.parchmentmc.org" }
	maven { url = "https://maven.ladysnake.org/releases" }
}

dependencies {
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings(loom.layered {
		it.mappings("org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.qm_version}:intermediary-v2")
		it.officialMojangMappings { nameSyntheticMembers = false }
	})
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// deps
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
	modImplementation(include("maven.modrinth:midnightlib:${project.midnightlib_version}"))

	// compat
	modCompileOnly("dev.emi:trinkets:${project.trinkets_version}") { exclude(group: "com.terraformersmc") }
	modCompileOnly("maven.modrinth:automobility:${project.automobility_version}")
	modCompileOnly("maven.modrinth:templates-2:${project.templates_version}")

	// dev
	modLocalRuntime("dev.emi:trinkets:${project.trinkets_version}") { exclude(group: "com.terraformersmc") }

	modLocalRuntime("maven.modrinth:automobility:${project.automobility_version}")
	modLocalRuntime("maven.modrinth:jsonem:${project.jsonem_version}")
//	modLocalRuntime("maven.modrinth:templates-2:${project.templates_version}")

	modLocalRuntime("com.terraformersmc:modmenu:${project.mod_menu_version}")
	modLocalRuntime("maven.modrinth:here-be-no-dragons:1.0.0")
}

tasks.register("buildOrPublish") {
	group = "build"
	String mavenUser = System.getenv("MAVEN_USER")
	if (mavenUser != null && !mavenUser.isEmpty()) {
		dependsOn(tasks.named("publish"))
		println("prepared for publish")
	} else {
		dependsOn(tasks.named("build"))
		println("prepared for build")
	}
}

processResources {
	exclude("assets/bingbingwahoo/icon_compressed.png")

	Map<String, Object> properties = [
			"version": version,
			"loader_version": loader_version,
			"fabric_version": fabric_version,
			"minecraft_version": minecraft_version,
			"midnightlib_version": midnightlib_version
	]

	properties.forEach((k, v) -> inputs.property(k, v))

	filesMatching("fabric.mod.json") {
		expand properties
	}

	filesMatching("bingbingwahoo.mixins.json") {
		expand "archivesBaseName": archivesBaseName
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.set(17)
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			url = "https://mvn.devos.one/${System.getenv("PUBLISH_SUFFIX")}/"
			credentials {
				username = System.getenv("MAVEN_USER")
				password = System.getenv("MAVEN_PASS")
			}
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "p7ksDUGo"
	versionNumber = version
	versionName = "Bing Bing Wahoo " + version
	versionType = "release"
	changelog = readChangelog()
	uploadFile = file("build/libs/${remapJar.archiveBaseName.get()}-${version}.jar")
	gameVersions = compatible_minecraft_versions.split(",").toList()
	loaders = [ "fabric", "quilt" ]
	dependencies {
		required.project("fabric-api")
	}
}

String readChangelog() {
	String text = Files.readString(project.file("CHANGELOG.txt").toPath())
	String[] split = text.split("----------")
	if (split.length != 2)
		throw new IllegalStateException("Malformed changelog")
	return split[1].trim()
}
